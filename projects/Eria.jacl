#!../bin/jacl

# Eria
# By Parham Doustdar and Robert Osztolykan

# CONSTANTS FOR RESOURCES IN BLORB FILE
constant sound_intro 1

constant game_release 1
constant game_build 1
constant game_title "Eria"
constant game_author "Parham Doustdar and Robert Osztolykan"

#Region stuff
constant region 13
constant Other 0
constant Cellars 1

attribute WEAPON WIELDED

{+intro
volume 20
sound sound_intro
clear
write game_title ^
write "By " game_author ^
write "Release " game_release ^
write "^----------^"
print
You always have liked listening to  music on your 64-Bit AMD computer, which you spent quite a lot of money on. A selection of music, given to you by your
friends but mostly, by Beata.^
She has always supplied you with her music (and some other things) and she has been the one you have loved. Nothing to do about it, though, because she's gone on a trip with her family. She will be back tonight and you have no school for tomorrow so you can talk to her for a day. A full day! And you, as always, opened up a fantasy book and started reading. You will always like these kind of stories, just as you'll always love Beata.^
Letting your mind drift into the story, with the soft music playing in the background, you lose yourself in the world that the book weaves for you. You can see each blow, each sword contact, you can feel the hero's pain, you can feel the hero's fear, you can feel the hero's heart, because you are the hero. Hero of a fantasy. Even though it is for a short time... still, you are a hero.^
A beeping sound brings you out of your story reading. You realize that it's already 1:00 AM and you need to get into bed because Beata would be alone at 8:00 AM tomorrow morning and you'd have to talk to her. Or you'd miss her. Forever. You wonder where that ~forever~ word came from. Forever? No, you'd be able to talk to her the day after, or the day after, but something, some... yes, thing, seems to disagree in your head. ~Not really~, it seems to mock. A strange sensation shoots through your body, and it seems to go all the way from your scalp around your head, and then down your spine to the tip of your toes.  Prickles, perhaps. Is it fear? Are you feeling something wrong? You forget these thoughts as you get onto your bed and roll around, until sleep overtakes you.^
In the dream, you find yourself in a vast chamber, surrounded by hard iron walls. This would be the end of you for reading so much fantasy, after all.^
Your vision blurs. Everything blurs. Maybe you are waking up? But why is it taking so long?^
The walls soar up out of sight to nothingness.  Where you are going, is deep underground. Everywhere the ceiling and floor are made of iron. At least, you guess it should be iron. It seems nothing like that, though. As your vision of the building blurs, you see lots of cells, with locks as big as yourself. How someone can lift those locks is a mystery you would think about when this damned blurring ends! At last, when you were going to get a headache for the rest of your life, the blurring ends, and you are in the middle of a lonely intersection, somewhere quite dark. But strange as it is, there is light without a light source here. The lock is almost as thick as you, and taller, but you would be able to touch the top of it if you raise your hand. So just for a try, you do.^
As you touch the lock, something runs through your body; it's like you've touched a six hundred volt electrical device. You long to scream, but you don't want to be soft, even in a dream. You'd endure whatever happens, until death. Suddenly, you are inside a cell; you don't know whether it's the same cell. You gasp as you look around.^
.
look
}

location cell: cell
short the "cell"
region Cellars

{look
write "This is a typical cell, with completely unmarked floor and ceiling, and the only piece of furniture is a bed in the northwestern corner. The door of the cell stands "
if here(south) = nowhere
write "closed "
else
write "open "
endif
write "behind you.^"
if drawing(parent) = here
write "A drawing on the east wall catches your eye.^"
endif
if @girl.tell_about_john = 0
print
A girl, around your age, lies on the floor. Her skin is torn, blood is running down her beautiful face and cheeks, and her skin has come off in some points. With sudden embarrassment, you realize that she's wearing nothing. You don't know whether she's asleep or awake. Or even whether she's alive. But you hope she is. You just hope. But hoping does nothing in the middle of a dream. Dreams are wishes. Who knows if such a pretty girl ever existed? But something doesn't feel right;
this dream is different. For a try, you slap yourself, and want to yell when you feel the pain. But this can't be. You should be asleep, on your bed, in your bedroom, waiting for the morning to come. A cough brings you out of your thoughts.^
You notice that some little drops of fresh blood are running down the corner of her mouth, and then her right cheek.^
~Who are you?~, she demands.^
.
style note
write "Type TALK TO GIRL to talk to the girl and answer her questions if you so wish. You may want to type ~converse with <person>~ to talk with them continuously, and enter zero (0) to exit the conversation mode.^"
style normal
set MENU_MODAL = true
execute +menu_prompt<talk_to_girl
endif
}

object bed: bed
mass 100

{examine
if @bed.lie_on = 0
write "The bed is nothing more than a piece of thin cloth and four legs that hold it upright. Beneath the cloth, visible through the thin fabric, is an uncomfortable-seeming piece of thick wood.^"
else
write "It was once an uncomfortable bed, but now it's not even that. The four legs, supporting it, or, well, supposed to be supporting it, are now lying around the bed itself, and from the bed, only a piece of wood remains, which still seems to be too heavy to move. It seems pretty useless, anyway. You wonder if the girl ever lay on it.^"
endif
}

{lie_on
if girl(parent) = here
write "The girl glares at you as you go towards the bed, not paying attention to her questions.^"
else
write "You sit on the bed, turning slightly to the right and lifting your feet off the ground, leaning your weight on your left elbow. As soon as your feet are off the ground, the squeeking bed finally gives out, the mattress under you tilting backward, dropping you head-first "
if bed_position = 0
if @bed.pull = 0
write "into a pit you had never spotted before with a muted, metallic thud!^"
else
write "into the pit below!^"
endif
set pit2(up) = here
move player to pit2
else
write "onto the ground below you, making your head ring and your eyes water. After a little while, you sit up, dizzy from the force of impact.^"
ensure player has SITTING
endif
endif
}

integer bed_position
grammar push *present >push

{pull
if girl(parent) = here
write "The girl glares at you as you go towards the bed, not paying attention to her questions.^"
else
if bed_position = 0
ifall @bed.look_under = 0 : @ = 1
write "You pull the bed aside, revealing a pit beneath!^"
else
write "You pull the bed to the other side of the room"
if @ = 1
write ".^"
else
write " again.^"
endif
endif
set here(down) = pit2
set pit2(up) = here
set bed_position = 1
else
write "It's already pulled to the other side. You need to push it back if you want to move it.^"
endif
endif
}

{push
if girl(parent) = here
write "The girl glares at you as you go towards the bed, not paying attention to her questions.^"
else
if bed_position = 1
write "You push the bed back to where it was before.^"
set bed_position = 0
set here(down) = nowhere
set pit2(up) = nowhere
else
write "It's already pushed against the wall!"
if @ = 1
write " You need to pull it back if you want to access whatever that is under it.^"
else
write ^
endif
endif
endif
}

{move
if bed_position = 0
execute bed.pull
else
execute bed.push
endif
}

{look_under
if bed_position = 0
write "You see something glinting from below the bed. Moving your head further under it, you spot the lock of what seems like a trapdoor lying there, unlocked, upon the ground. The trapdoor it was supposed to be keeping shut is nowhere to be seen. There seems to be an open space under the bed, but you don't have access to it with the bed restricting your movements.^"
endif
}

{crawl_under
if bed_position = 0
write "You crawl under the bed, lowering yourself carefully into the open space below the bed.^"
set pit2(up) = here
move player to pit2
else
write "Um, what would the point be?^"
endif
}

object leg: leg of the bed
short a "leg of the bed"
long "A piece of metal, sharp on one end and wide on the other, lies on the ground. It used to be one of the legs of a bed.^"

{examine
write "This is one of the legs of the bed that has been cleanly cut off the frame of the bed itself.^"
}

object drawing: picture drawing 
has NOT_IMPORTANT
short the "drawing"
parent limbo

{examine
write "It's a perfectly drawn picture of a fully blossomed flower, with four petals surrounding it. Strangely, the flower is indicated with an open lock, and the petals each have the symbol of one of the main four elements; ~fire~, ~water~, ~air~ and ~earth~.^"
write "Below it is written, ~Patience, trial and error are the keys to find the answer to this lock. To see if you have guessed right, push the flower.~^"
}

object fire_symbol: symbol of fire
short the "petal having the symbol of fire upon it"
parent drawing

{examine
write "This is a petal, and the symbol of fire is drawn in it's center.^"
}

{push
if fire_symbol(status) = 0
set fire_symbol(status) = 1
write "You touch the symbol with the tip of your finger, and it grows warm under your hand, emiting a strange, red light.^"
else
write "You have already touched it.^"
endif
}

{feel
execute fire_symbol.push
}

object earth_symbol: symbol of earth
short the "petal having the symbol of earth upon it"
parent drawing

{examine
write "This is a petal, and the symbol of earth is drawn in it's center.^"
}

{push
if fire_symbol(status) = 1
if earth_symbol(status) = 0
set earth_symbol(status) = 1
write "As you touch this petal, it adds a red light of it's own to the one coming from the other, mingling with it.^"
else
write "You have already touched it.^"
endif
else
write "Nothing happens.^"
endif
}

{feel
execute earth_symbol.push
}

object water_symbol: symbol of water
short the "petal having the symbol of water upon it"
parent drawing

{examine
write "This is a petal, and the symbol of water is drawn in it's center.^"
}

{push
if fire_symbol(status) = 1
if earth_symbol(status) = 1
if water_symbol(status) = 0
set water_symbol(status) = 1
write "Another red light joins the others at a point above the drawing of the open lock, and you feel the wall getting warmer just before you remove your finger from the symbol of water that you touched.^"
else
write "But you have already touched it.^"
endif
else
write "As you touch the petal, the glow from the one with the symbol of fire goes out, signaling that you have touched the wrong one.^"
set fire_symbol(status) = 0
endif
else
write "Nothing happens.^"
endif
}

{feel
execute water_symbol.push
}

object air_symbol: symbol of air
short the "petal having the symbol of air upon it"
parent drawing

{examine
write "This is a petal, and the symbol of air is drawn in it's center.^"
}

{push
if fire_symbol(status) = 1
if earth_symbol(status) = 1
if water_symbol(status) = 1
if air_symbol(status) = 0
set air_symbol(status) = 1
write "As you touch the last petal, a strange hum sounds deep within the wall, and a fourth light mingles with the rest, creating a wonderful, illuminating light that makes you smile with satisfaction.^"
else
write "But you have already touched it.^"
endif
else
write "As you touch this petal nothing much happens, except the other petals stop glowing, signaling that you should start from the beginning.^"
set earth_symbol(status) = 0
set fire_symbol(status) = 0
endif
else
write "The light from the other petal goes out, making you understand that you should start all over again.^"
set fire_symbol(status) = 0
endif
else
write "Nothing happens.^"
endif
}

{feel
execute air_symbol.push
}

object lock_symbol: symbol of lock flower
short the "flower having an open lock upon it"
parent drawing

{examine
write "This is a flower, the center of the drawing, and an open lock is drawn on it.^"
}

{push
if fire_symbol(status) = 1
if earth_symbol(status) = 1
if water_symbol(status) = 1
if air_symbol(status) = 1
move box to here
write "You gingerly touch the flower with the tip of your finger, and your finger almost gets burned in the process. With a loud bang, something falls onto the floor from above.^"
else
write "The petals stop glowing.^"
set water_symbol(status) = 0
set earth_symbol(status) = 0
set fire_symbol(status) = 0
endif
else
write "The petals stop glowing.^"
set earth_symbol(status) = 0
set fire_symbol(status) = 0
endif
else
write "The petal with the symbol of fire on it stops glowing.^"
set fire_symbol(status) = 0
endif
else
write "Nothing happens.^"
endif
}

{feel
execute lock_symbol.push
}

object box: box
has CLOSED CLOSABLE CONTAINER
mass heavy
short a "box"
long "A box, made of the same material you see everywhere lies on the ground.^"
parent limbo

{examine
write "It's a box, made of a transparent colorless material, like the rest of this place, although there is one tiny difference, which appears huge to your eyes that are so used to the oneness of the whole place: it is in a darker shade. It doesn't still have a color, exactly, but it still seems not to reflect the light as much as other things do."
}

object ball: mystical ball
short a "round, glowing ball"
parent box

{examine
write "Clouds of smoke seem to swirl beneath the surface, drawing you, mesmerizing you, pulling you within...^^"
write "All around you is darkness. Unbearable, unpenetrable, consuming darkness. Through it all, there is just one thread of sanity, a small glimmer of hope: the girl. She invites you, pulls you in, calls to you. ~Come to me.~. Her sweet, musical tone pierces the darkness, cutting through it like a hot knife through butter. Her unseeable arms open, as if wanting to draw you into an embrace, as if to make you pass through the nothingness unscathed, sane. But is it possible, you wonder. ^"
write "You smile, but it has no meaning. Nothing has a meaning. It's like your soul is hovering in a point of the world that has nothing to offer, nothing to take, but your soul.^It is like you are being sucked to eternal darkness, eternal doom. You don't dare move in the darkness, being afraid of going the wrong way, and although from far and hardly audible -as if you're under the water- come the sweet words of comfort and hope, you can't move; "
write "you are pinned to the spot, paralysed with undescribable fear. There's nothing to fear but the darkness; it's evil nature, enfolding you like robes of evil, wanting to pull you in and hold you there until your soul is even more lost in the nothingness. In the limbo. In hell. A hell with even no fire to give you hope within it's burning depths. A hell with invisible demons that grope for your sanity, for your hope, for your soul. For you.^^"
write "~Come to me.~ a voice calls again softly, from beneath, above, and all around you. A voice you know too well. A voice you have heard a thousand times in your dreams. ~Let me free you, my love, from the cold hands of Keringar.~ it doesn't come to you, right at that moment, that you have been called ~my love~ or that you don't even know what ~Keringar~ is, but it resonates something deep within your core, as if it meant to be that way all along. As if you were meant to be hers, and she was meant to free you from the cold hands of Keringar. As if you knew. As if you wanted it to happen. "
write "As if your soul called to it's destiny, called to it's home. Called to the girl. Called to... Eria. You know her name now. ~Eria~. Tears dwell in your eyes, your unseeing eyes, your non-existent eyes, as the name comes to you. Had you known her? Had you been with her? And then, within the grasp of Keringar, you remember it all. You remember a hundred years ago, or a thousand, as you closed your eyes one last time, and you remember the last words you heard: ~You are the gem, my wonderful Lenio. You are the destiny of the whole being. God is dead. He doesn't care anymore. You should save the world.~^"
write "The gate to your previous life opens, flooding you with memory, and Linio, your former self, the true you, returns, bringing with it a thousand sweet memories of love. Love for life, love for death, love for the world, but without Eria, none would matter. So above it all, love for Eria. Linio washes over you, making you whole, making you what Eria has seen for the first time in the darkness of Keringar, that you were indeed her lover, gone to be reborn and save the world. the one who gave the world up, who gave her up, for others, and now, it was time to reunite.^"
write "A spark of magic, so faint to be dismissed, but a spark nonetheless, stretches from you to Eria, and your heart goes out to her, through the darkness, through the barrier between the worlds, and the darkness moves aside, showing you the light that comes through, a hope, an opening, a way to go out. So you move with an incredible speed through the opening, invisible hands pushing you, or drawing you, through the passage, and then, you see her. You see her face, broken but smiling, lit up by your love, with eyes that blaze with need and happiness. But then, she disappears. You let out a soft, hopeless sigh of loss through parted lips, and realize that you finally have a body. At that instant, you hear her again: ~My love, this place is pro...~ and then you are flung backwards and upwards, and the next thing you know, you are flying...^"
write "And for the second time, the ground is rushing up to meet you, the difference being that this time, you don't even have time to go into a protective stance, and hit the ground face-first, and again, fingers of darkness, much different from the unsettling darkness of Kelingar, reach for you, engulf you, pulling you down to unconsciousness...^"
write "To be continued..."
more
end
}



object girl : girl eria
has ANIMATE FEMALE
short the "girl"
long function
mass 99
parent cell

{long
if this(parent) = cell
write "The girl that just talked to you is lying on the ground before your feet.^"
endif
}

integer yes_or_no

{tell_about_john
if @ = 1
write "~I'm John Bringston.~, you say uncertainly. ~You brought me here?~.^"
write "~Me?~, the girl says, smirking, as if finding the idea incredulous and stupid. ~Do I really seem that stupid?~^"
getyesorno yes_or_no
if yes_or_no = 1
write "~Yes.~, you say, frowning at her.^"
write "The girl chuckles and shakes his head, with an expression that seems to almost be of amusement.^"
else
write "~No...~, you say in a small voice.^"
write "A strange expression of what seems to be admiration, or maybe, sorrow, fleets through her face, chased by a stony, expressionless mask. ~Well at least you know that much.~, she declares, with no hint of what you thought showed on her face.^"
endif
set MENU_MODAL = false
set MENU_IN_LOOP = false
else
write "~Yeah, yeah...~, the girl says impatiently, as if out of time. ~You've already told me about that.~. The impatience on her face, though, is also expectant, as if she's waiting for you to ask a certain question.^"
endif
}

{ask_about_girl
if @ = 1
write "~Well, who are you?~, you ask, slightly confused.^"
write "~Who I am or what I am is not important right now.~, she responds, with a tone of finality. You sense, no matter how she is reduced right now, this girl used to have a lot of power and authority. "
if @tell_about_john_girl = 0
write " ~Don't you want to tell me who you are, though?~"
endif
write ^
else
write "~I said it's not important.~, the girl says, her cracked voice rising slightly."
if @tell_about_john_girl = 0
write " ~Don't you want to tell me who you are, though?~^"
else
write ^
endif
endif
}

{ask_about_home : ask_about_returning
if @tell_about_john_girl = 0
write "The girl pouts just a little. ~Where are your manners?~, she says, looking at you teasingly. ~You might want to answer my question first?~^"
else
write "~Well anyway, can you get me back home?~, you ask her hopefully.^"
write "~Of course.~, she says, linking her hands together above her stomach. ~And I hope I won't see you ever again.~. Despite the harshness of her words, there is a slight undertone of sincerity in her voice; as if she means good although she is putting it in harsh words. And while you are pondering this, a current of energy that seems to come from her raises you up into the air, and pushes you out through the door, and back through the way you came. "
print
You think, out of nowhere, it is very very important to know the word that is written by the side of the door of her cell, but no matter how you try, you can't see it. You squint, but then suddenly you are pushed downwards. You manage to catch the words by the cell near you, although they make no sense. ~AAY~. And then, you seem to be sucked inwards along with being pushed outwards, and as you fly up with a sickening speed and then go southwards, you abruptly stop before a door and fall to the ground, the energy pushing you, and the one pulling you both gone. You sense the pushing energy trying to move you again, but it doesn't work. ~Nooooo! Let him go, please!~, you hear the girl say, and then hear her sob in the distance, although how you can hear it is again, a question. But at least one question is answered: she brought you here. How or why is not known to you, but the fact that she is powerless to get you out is pretty clear. And you know one thing as clear as the shining of the sun in a sunny day: you have to save her from whatever is going to happen to her. That is the way out; not the door behind you.^
.
set MENU_IN_LOOP = false
move girl to limbo
move john to chamber
execute "+menu_clear_options"
look
endif
}

{talk_to
execute "+menu_clear_options"
execute "+menu_add_option<Tell her about yourself.<tell girl about myself"
execute "+menu_add_option<ask her about herself.<ask girl about herself"
execute "+menu_add_option<Ask her if she can get you out of here.<ask girl about home"
}

{kiss
if @ = 1
write "The girl frowns harshly at you as you go to kiss her, pushing your lips away with the back of her hand.^"
else
write "I don't think she would appreciate that, she has shown you that before.^"
endif
}

{converse
execute +menu_prompt<talk_to_girl
}

object home : home
parent limbo

object returning : returning
parent limbo

location pit2: "open space under the bed"
short the "open space under the bed"
region Cellars

{look
write "You are in a circular pit, about six feet tall and two feet across. Something seems to be written on the wall at the height of your chest, and it is the only feature of the colorless walls that surround you.^"
}

object writing2: writing inscription
short the "writing"

{examine
write "Three numbers are written on the wall, each having a different color than the rest. In red is written the number ~8~, and the number ~4~ is in blue, while the last number, ~3~, is green.^"
}

{read
execute this.examine
}

location chamber : Chamber
short the "chamber"
west empty_room
north corridor
region Cellars

{look
print
The ceiling is made of iron. The floor is made of iron. The walls are made of iron. You feel you might be made of iron, too. But it shines in a way that iron doesn't. You let it go. It's just a kind of metal or something. As you peer around, you see a room to your west, and an exiting door to the south. Also, a way is leading out of  this chamber to the north.^
.
}


{first_hint
write "You don't need this door to get out. You do, but you wouldn't be able to open it right now anyway. Don't fiddle with it, let it go. You'll get to it in time.^"
}

integer ERDoor false

{movement
ifall compass = west : ERDoor = false
write "As you take a few steps to the west, a loud ~bang!~ sound makes you turn around. Strangely, a door that wasn't there before has now closed shut behind you!^"
      set empty_room(east) = nowhere
   endif
endif
if compass = north
   if corridor hasnt MAPPED
      write "As you enter this corridor, a liquid of fire pours out from a pit in the room north of you! Strangely, even this much heat (which you can feel from this distance) does not seem to burn the strange kind of metal which makes up the building you are in. The wall of fire blocks the way north and you'd probably burn if you go closer, however.^"
   endif
endif
if compass = south
   execute knob.turn
   return
endif
return false
}


object door: exit exiting door


{examine
print
It's a simple, thick door with not even a lock on it. In the center, though, a very plain knob stands, as if beckoning you to turn it.^
.
}

{open
execute knob.turn
}

{cut_with_dagger
if @ = 1
write "You take a few steps backwards, then run towards the door, with your dagger raised in front of you. The tip of the dagger greets the door with a clang, but other than that, the door doesn't even have a scratch on it...^"
return
else
write "Why bother? It wouldn't do you any good.^"
return
endif
}

object knob: knob


{examine
write "It's a plain, non-descript knob.^"
}

{turn
if @ = 1
print
You turn the knob but suddenly feel your head spinning from the pain and a voice saying: ~You are mine! No need to exit through the main door!~. As you hear an evil laugh, you start to curse with all your might and pound on the door a few times, but as you could've already guessed, there's no result.^
.
else
print
You wouldn't want to feel that great pain again. Maybe you can find another exit, instead.^
.
return
endif
}


location empty_room: big empty room
short the "big empty room"
east chamber
region Cellars


{look
write "Again, iron. Or whatever that is. Your footsteps echo in the empty room, and it makes such an annoying sound that makes you wince."
if hole(parent) = limbo
write " It's not smooth everywhere, though. Somewhere in the middle of the room, the floor is higher a little. "
write "It would've gone unnoticed, however, if not for the fact that your eyes are used to this smoothness."
else
write " A hole occupies the center of the room."
endif
if ERDoor = false
write " The door to your now-made cell stands to the east, blocking your way.^"
else
write " The only available exit leads eastwards, back to the chamber.^"
endif
}


{movement
if compass = east
if empty_room(east) = nowhere
write "But the door is in the way!^"
return
endif
endif
if compass != east
write "The only exit is to the east.^"
return true
endif
return false
}


{first_hint
if ERDoor = false
write "Look below your feet. When a portion of the floor is higher, even just a tad, there's a possibility that something is below that part. Try using that ~thing~. Remember, you don't have to remove that portion of the floor, just try using it. Does it have to be pulled? Pushed? Turned? Which one of these can you do when you don't have direct access to the object?^"
return
endif
if hole(parent) = limbo
write "Well, now that you are here, you might as well have some fun. Do you think there's a button under the place you jumped on? Well, now that it has opened the door, will it close it again? Do you want to find out?^"
endif
if hole(parent) = here
if dagger(parent) != limbo
write "What other hint do you want! You are done with this room! Well... for now.^"
else
write "Have you looked at the hole? Have you *really* looked at it? I think not.^"
endif
endif
}


object EDoor: "east door" door
short the "east door"


{examine
if ERDoor = false
write "It's simply the door to the room that was so simple, and has become your cell, now.^"
else
write "It has slid into the wall south of you, and only the tip is visible.^"
endif
}


object spot: higher portion spot floor section bump ground
short the "higher portion of the floor"


{examine
write "When you think a little more on that, you haven't seen such a spot on the floor since that blurring began. And you wouldn't have noticed it if you hadn't looked a lot. Also, it's not too much higher than the surface which makes it a little suspicious. Why should it be here?^"
}

grammar put my weight on *here >jump_on
grammar put your weight on *here >jump_on

{jump_on
if @ = 1
write "You put all your weight on the spot, and almost expectedly, something starts roaring deep within the ground under your feet, and the door slides back, revealing the exit.^"
write "As you step down from the higher part of the floor, you can feel the ground beneath your feet tilting slightly to the side, as if loose.^"
set empty_room(east) = chamber
set ERDoor = true
points 1
else
write "You jump on the higher spot of the ground just to see what happens, and accidentally slip and fall to the ground with a loud thud! When you stand up and dust yourself off, you realise that the higher point of the floor has come loose, uncovering a hole in the process!^"
move loose_point to here
move hole to here
move spot to limbo
points 1
endif
}

integer Shaped false

object loose_point: loose point piece of floor
short the "loose point of the floor"
long function
mass 10
parent limbo


{examine
if Shaped = true
write "Made of a strange kind of metal which seems impossible to be cut by anything, this armor is supposed to be a kind of protection for you.^"
else
write "It's a portion of the floor, almost as tall as you, if just a tad taller. It's also wider than your body, twice as wide as you, in fact.^"
endif
}

{wear
if noun1 has WORN
write "But you are already wearing it!^"
return
endif
if Shaped = true
write "Wrapping the piece of cloth around the cut metal, you put it on.^"
move cloth to limbo
ensure loose_point has WORN
else
if cloth is *held
if dagger has WIELDED
write "Cutting the metal, you make it your size, discarding the rest. Wrapping the piece of cloth you had torn from the coat around it, you finally have a set of one-piece armor, that seems to be cut only by this dagger.^"
ensure loose_point has WORN
move cloth to limbo
set loose_point(mass) = 5
set Shaped = true
points 1
else
if dagger is *held
write "Try WIELDing the dagger first.^"
set time = false
return
else
write "You need to hold something with which you can cut the metal and make it your size. It's too big for you!^"
set time = false
return
endif
endif
else
if dagger has WIELDED
write "You can cut the piece of metal to fit you, fine, but it would unwrap from around you if you try to wrap it around your body. You need to find something which would hold it and stop it from unwrapping.^"
set time = false
return
else
if dagger is *present
write "Try WIELDing the dagger first.^"
set time = false
return
else
write "You can't wear it this way. It's too big for you!^"
set time = false
return
endif
endif
}

{remove_override
set player(quantity) - noun1(mass)
ensure noun1 hasnt WORN
write "Unbuckling the piece of cloth, you remove the piece of metal.^"
move cloth to player
}



{long
if Shaped = true
write "A piece of metal lies on the ground.^"
else
write "A piece of the floor lies here.^"
endif
}

object dagger: dagger knife
short a "dagger"
long function
parent limbo
mass 5
has WEAPON

{examine
write "So sharp, and so nicely-balanced piece of art."
if this is *held
write " It almost has no weight in your hand, and although you don't know anything of swords and daggers, this one really should be something of great value."
if @ = 1
write " As you touch the floor with the tip of the dagger in wonder, you realise that it cuts the material which makes up this building!^"
else
write ^
endif
else
write ^
endif
}

{long
write "A sharp, seemingly dangerous dagger lies here, glinting and seeming to shine of it's own accord.^"
}

{wield
if dagger has WIELDED
write "But you are already wielding it!^"
else
write "You grip the dagger tightly.^"
ensure dagger has WIELDED
endif
}

{unwield
if dagger hasnt WIELDED
write "But it's already sheathed!^"
else
write "You sheathe the dagger, hoping you will never have to draw it again.^"
ensure dagger hasnt WIELDED
endif
}

object hole: hole
parent limbo


{examine
if dagger(parent) = limbo
write "It seems to have something hidden in it since it has some scraps of metal thrown in to it in an arranged way...^"
else
write "The scraps of metal that were previously arranged are now in a chaotic order, one of the effects of your searching efforts.^"
endif
}


{search : *move_scraps
if dagger(parent) = limbo
write "As you hastily search the hole, you feel something sharp cut your fingertip. Wincing slightly and sticking your finger into your mouth, you bend down and peer into the hole, catching a glimpse of something glinting. As you pull it out, you realize you're holding a dagger!^"
set dagger(parent) = player
points 1
else
write "No matter how much you try, seems there's nothing more in the hole.^"
endif
}


object scraps: scraps "scraps of metal" scrap
short some "scraps of metal"
parent hole
has PLURAL

{examine
write "They are, as you guess, just some scraps of useless junk, which are "
if dagger(parent) = limbo
write "in a neat order.^"
else
write "haphazardly thrown around.^"
endif
}

{take
write "You feel taking some scraps of metal wouldn't advance you in any way.^"
}

{look_under
write "The hole is completely filled up and you can't get a clear view of what is below the scraps of metal; if anything. Something, however, seems to be glittering amongst the contents.^"
}


object clothes: clothes coat
short your "clothes"
has WEARABLE WORN PLURAL
mass 1
parent john

{examine
if loose_point has WORN
write "The piece of armor you are wearing prevents you from seeing the coat beneath.^"
return
endif
write "You haven't seen such a thing in your whole life, other than in fantasy movies. It's a coat with it's sleeves guilded heavily with gold and silver, the front is full of golden lines crisscrossing each other, making a great view of gold work. The coat itself is made of pure cloth, one you have never seen even in those movies. It is cloth, but it's so shiny it seems to be made of gold itself."
if corridor has MAPPED
write " What matters most of all, though, is that there are no colors in it, and you realize that in the world you are sleeping in right now, this coat would be expensive, yes, but not the colorless cloth, perhaps. Only the gold and silver decorating it could be used for anything"
if @self.tear >= 1
write ", and Part of it's sleeve is torn, anyway.^"
else
write ".^"
endif
else
if @self.tear >= 1
write " Part of it's sleeve, however, is torn."
endif
write ^
endif
}


{tear
if @ > 1
write "But you've already torn it!^"
return
endif
write "Reluctantly, you tear a part of the coat's sleeve.^"
set cloth(parent) = player
}


object cloth: small cloth of piece torn
short a "piece of cloth"
long "A piece of cloth lies on the ground.^"
mass 0
parent limbo


{examine
write "It's a piece of cloth, torn from a coat that seems to be very expensive.^"
}

location corridor : Corridor
short the "Corridor"
south chamber
north midair
region Cellars

{look
write "This corridor stretches to north and south, and you remember this room as the first room you saw when your sight started blurring. Yes. It all began from the chamber south of here. Other than that, the room is naked, empty, and bare, like the rest of the place."
if here hasnt MAPPED
write " The whole place seems like someone has built it quickly, for a purpose, and didn't bother to add extra things. Thinking of it, you can't even see a color in anything, other than your clothes, that is. And what color are they exactly? You look down, and see "
if loose_point hasnt WORN
write "your clothes. Other than the thin strands of gold and silver running through it, nothing else in it has color. You suddenly don't like that coat anymore. It has a lot of gold and silver, alright, it is shiny, true, but where are all the colors?^"
else
write "the armor covering your clothes. ~What the heck ever~, you say, shrugging, or at least trying to shrug. The armor is so heavy on your shoulders, you can barely move.^"
endif
else
write " You can see your own image in the wall of flames north of you. Something tells you this fire is not a usual one.^"
endif
}

{first_hint
if loose_point hasnt WORN
write "Notice how the fire doesn't burn the material that makes up the floor and the walls? Maybe you can wrap a piece of it around your body as protection?^Of course, only if it's your size. "
if Shaped != true
write " But first, you have to make it your size, of course.^"
else
write ^
endif
endif
if dagger is *held
write "You are carrying the necessary tool to reduce it's size.^"
else
write "Unfortunately you are not carrying the tool to reduce it's size. Maybe you missed something in one of the rooms.^"
endif
else
write "You have already solved the puzzle by wearing the loose point of the floor you found in the empty room. You can move on.^"
endif
}

{movement
if compass = north
if loose_point has WORN
write "Stepping confidently through the wall of fire, it occurs to you right in the last instant that your feet, arms and face are not protected! Coming out the other side alive and intact, you think you caught a glimpse of a blue aura around you, but the fact that it is not there anymore and you caught just a glimpse of it, (that is if you did), makes you uncertain. Looking down though, you see your shoes are also intact, and the piece of cloth around your so-called armor should've been burned by now -- which, it is not.^"
write "And as you stand facing the pit of fire in silent wonder, you notice neither the creaking of the floor, nore the bending of the very thin metal beneath you. It is only at the time you lose your balance that you realize what has happened. There are no walls around you, and the floor under your feet has given in to your weight. As your feet slide out from under you and you slide down, gaining momentum every second, the thought that where you are going finally drives home. Actually, slams home would be a better phrase, because the sudden realization makes you look around, and as you do, you suddenly know. You are falling down. It is the only thing that comes to mind, and even when you wait for your mind to continue the sentence further, perhaps to let you know where you are going, or how long the fall is, the voice inside your head expectedly stays quiet.^"
return false
else
write "As you step into the wall of fire, a lot of things happen in the space of less than a second. First nothing happens, other than a blue glow surrounding you, coming out of nowhere. Then, you begin to get warmer as the heat begins to seep through. Just before you can get out from the other side, you give in to exhaustion, and the blue glow which seemed to feed from you disappears, leaving you unguarded, and... burning.^"
execute +game_over
endif
endif
return false
}

object pit : liquid fire pit flame flames
has NOT_IMPORTANT
short the "fire pit"

{examine
write "It's a circular opening in the floor, and liquid fire pours out of it with such intensity that you can see the tongues of flame licking the ceiling. It makes an almost thick wall of flame that blocks your way, extending from wall to wall, and floor to ceiling.^"
}

location midair: in midair
short name "In mid air"
has MID_AIR
region Cellars

{look
write "You are"
if curled_up = true
ifstring words = ""
write ""
endif
ifstring words = "curl up"
write " curled up,"
endif
ifstring words = "bend knees" : words = "bend my knees" : words = "bend your knees"
write "bending your knees,"
endif
ifstring words = "go limp"
write "limp,"
endif
ifstring words = "relax"
write "completely relaxed,"
endif
ifstring words = "get ready for impact"
write "ready for the impact,"
endif
endif
write " looking down."
if ground_distance = 1
write " Right now, you can't even see the ground below you!^"
endif
if ground_distance = 2
write " You can see the ground rushing towards you with a very frightening speed.^"
endif
if ground_distance = 3
write " The ground is very close to you now, and it looks like you would hit it in a few seconds.^"
endif
}

integer curled_up
integer ground_distance 1

{eachturn
if ground_distance = 2
write "The ground comes to view, rushing towards you as if impatient to see you hit it and turn into a thousand pieces of meat and bone.^"
endif
if ground_distance = 3
write "The ground draws ever closer, and you feel you are going to hit the ground in a few seconds, "
if curled_up = true
write "hoping your current protective position would save you.^"
else
write "turning your eyes skywards and thinking what to do.^"
endif
endif
set ground_distance + 1
if ground_distance = 4
if curled_up = true
write "Readying yourself for the impact and gritting your teeth, you hit the ground with a loud thud, collapsing to a heap, unconscious.^"
points 1
more " "
clear
style bold
write "**********Some Time Later**********^"
style normal
more " "
clear
write "You come to with a start. But again, instead of the comfortable look of your bedroom, the only thing that meets your eyes is two, no, actually, three colorless, confusing, endless walls, that still stretch to nowhere. Getting up to your feet and groaning with pain, you lean against the east wall, and glance around your surroundings.^"
move john to guard_room
look
else
write "Knowing that you have no chance to stay alive, you still don't lose hope as you hit the ground, and you have time to hope that you would wake up even when the blackness engulfs you -- but you never do.^"
execute +game_over
endif
}

{first_hint
write "Get yourself ready for the impact somehow! You are going to hit the ground in a few seconds!^"
}


grammar curl up >curl_up
grammar get ready for impact>curl_up
grammar bend knees >curl_up
grammar bend my knees >curl_up
grammar bend your knees >curl_up
grammar go limp >curl_up
grammar relax >curl_up

{curl_up
if curled_up = true
write "But you are already doing your best!^"
else
execute +gather_words
ifstring words = "bend knees"
setstring words "bend your knees"
endif
if ground_distance = 1 : ground_distance = 2
write "Acting purely on instinct, you " words ", wanting to close your eyes as you get closer to the ground, but not daring to.^"
set curled_up = 1
else
write "With barely a fraction of a second left for you to act, you do a last resort and " words ", closing your eyes.^"
set curled_up = 1
endif
endif
}

location guard_room: guard room
short the "guard room"
north cells
region Cellars

{look
write "The room consists of a wooden chair, a wooden table, and a lot of debris that seem to be thrown around in no particular order. The most out-of-place feature in this room is a doorframe to the north that leads to the cells north of you, seeming to be left in the middle of construction. This is the first room you have seen here that is not empty. Colorless, but not empty."
if writing(parent) = here
write "The fading, uncovered writing is visible on the ground, it's texture seeming to be something like graffiti."
endif
if sitting_on_the_chair = true
write " To the north of you is the many number of cells that seem to stretch far out of sight, although you have a sensation that they wouldn't be as many as they seem.^"
else
write ^
endif
}

{eachturn
move doorframe to here
}

{movement
if compass = north
move doorframe to cells
return false
endif
return false
}

{first_hint
if writing(parent) = here
write "There's no help I can give you about the riddle, sorry. You have to find it out by yourself. It's easy, though. Only three letters...^"
else
write "Hmm... there is something here which the like you have seen before. Do the same. Sorry, I can't be clearer than this!^"
endif
}

object doorframe: "wooden door frame" door frame doorframe
has NOT_IMPORTANT

{examine
write "Completely smooth, colorless, and seeming to be quickly built. The doorframe is completed, there are even three hinges set on the door,"
if this(parent) = guard_room
write " two on the left-hand side and one on the right,"
else
write " one on the right-hand side and two on the left,"
endif
write " but the lack of an actual door and one hinge makes it seem like the person, or persons, had some work to do before the door was completed.^"
}

object chair: wooden chair

{examine
write "It is an empty, plain wooden chair. It even has no cushions or anything. It is amazing how anyone would be guarding anything here, with this very uncomfortable place to sit on. It's "
if cdrawn = true
write "drawn and ready to be sat on.^"
else
write "pushed against the table.^"
endif
}

integer cdrawn false

{push
if this.CheckIfSitting = true
write "How would you do that while sitting on it?^"
return
endif
if cdrawn = true
write "You push the chair so that it is now set against the table.^"
set cdrawn = false
else
write "The chair is not yet drawn!^"
endif
}

grammar draw *present >pull

{pull
if this.CheckIfSitting = true
write "How would you do that while sitting on it?^"
return
endif
if cdrawn = true
write "But the chair is already drawn!^"
else
write "You draw the chair away from the table, so that it faces the cells to the north of you.^"
set cdrawn = true
endif
}

{move
if cdrawn = true
execute this.push
else
execute this.pull
endif
}

integer sitting_on_the_chair false

{sit_on
if cdrawn = true
if sitting_on_the_chair = false
write "You sit down on the chair, the whole maze of cells under your gaze.^"
set sitting_on_the_chair = true
ensure player has SITTING
else
write "But you are already sitting on it!^"
endif
else
write "You can't sit on the chair unless you draw it away from the table.^"
endif
}

{+stand
if sitting_on_the_chair = true
write "You stand up from the chair.^"
set sitting_on_the_chair = false
ensure player hasnt SITTING
else
if player has SITTING
   write "Using your arms to push yourself up, you stand.^"
   ensure player hasnt SITTING
   return true
endif
write "You are already standing.^"
endif
}

{CheckIfSitting
if sitting_on_the_chair = true
return true
endif
return false
}


object table: wooden table
has NOT_IMPORTANT

{examine
write "The table is plain and although it has no color, it's visible that no one has ever used it. There are no lines, no scratches, no handprints... nothing. It is so clean and strange that you don't even dare touch it yourself.^"
}

object debris: debris pieces garbage trash


{examine
write "The pieces of debris remind you of the ones that you found in the room near the chamber. You can't exactly remember where it was, your memory before your fall is a little hazy, but it doesn't matter now... does it?^"
}

{clean : search : move
if @ = 1
print
You delve into the debris with your hands, desperately trying to find something valuable. It seems that none of the things would be of any use to you and you almost give up, and that is when you notice some writing on the ground where you've cleaned off the debris.^
Pushing some useless trash aside, you manage to uncover the whole writing and begin reading:^
.
move writing to here
execute writing.read
else
write "You have already checked out everything. Beside the writing, you don't think you will find any useful thing, no matter how hard you try.^"
endif
}

object writing: writing
short the "writing on the ground"
parent limbo

{examine
write "It is a writing, and strangely enough, it's written in English -- in an awful handwriting.^"
}

{read
print
A strange thing hangs by a mans thigh, hidden by a garment. It has a hole
 in its head. It is stiff and strong and its firm bearing reaps a reward.^
When the man hitches his clothing high above his knee, he wants the head of that hanging thing to poke the old hole (of fitting length) it has often filled before.^^
.
write "The last line reads, "
style bold
write "~If you find the three-letter answer to this riddle, you will be led to what you seek. When you think you have found the correct cell, just try to touch the lock like you did before.~^"
style normal
}

location cells: cells
short the "cells"
south nowhere
region Cellars

integer cx 0
integer cy 0
integer cz 0
string letters A B C D E F G H I J K L M N O P Q R
string letters S T U V W X Y Z

{look
write "You are in a huge open area. Looking around you, you find out that you are engulfed in some kind of maze, constructed of heart-breaking strong, iron doors with locks as tall as you everywhere."
execute here.manipulate_doorframe
if doorframe(parent) = here
write " The doorless, half-completed frame stands to the south of you, leading back to the guard room.^"
else
write ^
endif
if here hasnt MAPPED
print
You wonder, how will you unlock them? But why do you need to unlock them at all?^
And then, with a flash of enlightenment, you remember. How your dream began, the pain, how you met the pretty girl, how she tried to stop this dream... but it seems she failed. Actually, she certainly did. If she hadn't, you would've been awake by this point, happy and fine in your bedroom, or already in your bed. What time is it, you wonder. Have your parents wondered where you are now, or your body is still there, sleeping? Have they moved you to the hospital? Have they thought you are dead? What about Beata, what is she thinking, is she crying? Is she awake? ~Now, why does it matter anyway,~ a voice asks inside your head, sounding strangely defensive and worried. ~the only thing you can do right now is to find her and get out of here. Quickly, if you please.~. The change of tone in that voice from the hidden source, the voice of thoughts that everyone has in their head, from defensive to amused and teasing makes you let out a frustrated, sad, lost sigh. What about that girl, now? She is probably in an even bigger trouble than you. You feel, no,  you know, she must be in one of these cells. Now, there is only one step left: get to the cell.^
.
endif
write "On the lock of the cell nearby, you see the letters, ~" letters[cx] letters[cy] letters[cz] "~.^"
}

{manipulate_doorframe
if cx = 0 
if cy = 0
if cz = 0
set here(south) = guard_room
move doorframe to here
return
endif
endif
endif
move doorframe to limbo
}

{movement
if compass = north
set cy + 1
if cy > 25
set cy - 1
write "There are no more cells in this direction.^"
return
endif
look
endif
if compass = south
set cy - 1
if cy < 0
set cy = 0
ifall cx = 0 : cz = 0
move player to guard_room
look
return
endif
if cx > 0 : cz > 0
write "There are no more cells in this direction.^"
return
endif
endif
look
endif
if compass = east
set cx + 1
if cx > 25
set cx - 1
write "There are no more cells in this direction.^"
return
endif
look
endif
if compass = west
set cx - 1
if cx < 0
set cx = 0
write "There are no more cells in this direction.^"
return
endif
look
endif
if compass = up
set cz + 1
if cz > 25
set cz - 1
write "There are no more cells in this direction.^"
return
endif
look
endif
if compass = down
set cz - 1
if cz < 0
set cz = 0
write "There are no more cells in this direction.^"
return
endif
look
endif
if compass = northeast : compass = northwest : compass = southeast : compass = southwest
write "You can only go to the north, east, south or west.^"
endif
}

{first_hint
write "It's simple: find the cell that you think has the correct word written beside it, and then touch the lock. Bingo!^"
}

object lock:lock
short the "lock"
parent cells

{examine
write "They are almost taller than you, and wider. You wonder how many people it would take to open one of these locks. Other than their heavy look and plain surface, they are completely featureless."
if @ = 1
write " Maybe that writing meant to say something, after all. But what if touching a lock would result in the same pain which makes you shiver even now? You know that you don't have a choice. ~Who risks, wins.~, your father used to tell you. It's time to test his theory and hope for the best."
else
write ^
endif
}


{feel
if @wrong_guess_lock = 0
write "You utter a prayer and touch the lock of the cell nearby.^"
endif
if @wrong_guess_lock = 1
write "Still shocked by the pain you've encountered, you raise your hand and hesitantly touch the lock of the nearby cell.^"
endif
if @wrong_guess_lock = 2
write "Tears in your eyes, your body trembling, you realise that this is your last chance to guess correctly. Yours, and the other girl's."
write " Reluctantly, you raise your hand. Offering your life, you touch the lock of the cell nearby...^"
endif
ifall cx = 10 : cy = 4 : cz = 24
execute this.correct_guess
else
execute this.wrong_guess
endif
}

{correct_guess
write "As you touch the lock, you receive something like an electric shock."
if @wrong_guess_lock > 0
write " You let out a whimper of sorrow, thinking that you are going to be in pain again, and close your eyes. However, when nothing happens and you open them, you realize you are in an empty cell...^"
else
write " Remembering what happened in the beginning of what you thought was a dream, a soft smile touches your lips as you expectedly find yourself in the familiar cell...^"
endif
move player to cell
move drawing to east_wall
#execute +eachturn
proxy "look"
}

{wrong_guess
if @ = 1
write "You touch the lock and a jolt of electricity rocks your body. Unfortunately, though, when you look around you are in the same place, and you realize just a fraction of a second before the pain begins that this is the wrong lock! No matter how you try to pull your hand away, it seems to be sticking to the lock. As the pain increases, you scream, and after some time, the electricity stops abruptly, leaving some pain still in your body.^"
return
endif
if @ = 2
write "Again, as you touch the lock, you get a small shock compared to what you felt before, and again you allow your hopes to rise, and again you don't draw your hand away, and again you can hear the echo of your frantic screams reach your own ears. As the electricity stops again, you know that the next time would be your last if you don't guess right. You don't know how exactly you can know this, but you do, all the same.^"
return
endif
if @ = 3
write "For the third time, you get the small jolt of electricity, and this time you don't have the time to even flinch before a large wave of electricity runs through you, maybe turning you to ash, or turning you to a frozen, electrocuted figure which would be a lesson for others not to mess with big people. At any rate, your spirit is free, and in the moment before you die, you think maybe it is for the better that you don't wake up at all.^"
execute +game_over
endif
}

object john: myself self me
has		ANIMATE
short		name "yourself"
quantity	42
parent cell
player


{examine
if here(region) = Cellars
write "You cannot see yourself clearly as you look down. But you have a general idea of how you were before you came here. But come to think of it, since your clothes are changed, you are not sure whether you are still the same or not.^"
else
write "As beautiful as ever.^"
endif
execute "+inventory"
}

{cut_with_dagger
write "Ouch. You are not thinking of suicide, are you?^"
set time = false
}

{take
write "...seriously?^"
set time = false
}

{+no_light
write "It is too dark to see.^"
set time = false
}

{+title
if player has SITTING
write "(sitting)^"
endif
}

object north_wall: north north wall
short          the "north wall"
has NO_TAB PLURAL

object south_wall: south southern wall
short          the "south wall"
has NO_TAB PLURAL

object east_wall: east eastern wall
short          the "east wall"
has NO_TAB PLURAL

{examine
ifall player(parent) = cell : girl(parent) != cell
write "This wall is bare just like others, the only difference being the drawing that is drawn on it, seemingly by a chalk.^"
else
write "There is nothing special about the east wall.^"
endif
}



object west_wall: west western wall
short          the "west wall"
has NO_TAB PLURAL


object ground: ground floor
has            SURFACE
short          the "ground"
has NO_TAB


{examine
write "Smooth. In fact, very smooth. And shiny. Nothing much to say about it, just you think you will never know what it is made of."
if here = empty_room
write " A raised portion of it catches your eye.^"
else
write ^
endif
}


object ceiling: ceiling
short the "ceiling"


{examine
write "Like the walls and floor, the ceiling is made of a kind of metal, which, there's no need to say  you have not figured what it is."
}

string words

{+gather_words
setstring words ""
set INDEX = 0
while INDEX < @command
ifstring command[INDEX] = "my"
addstring words "your"
else
addstring words command[INDEX]
endif
set INDEX + 1
if INDEX < @command
addstring words " "
endif
endwhile
}

{+curl_up
if curled_up = true
write "But you are already doing your best!^"
return
else
execute +gather_words
write "You " words " and sit in a corner.^"
ensure player has SITTING
set curled_up = true
endif
}



grammar tear *held >tear

{+tear
if +important<noun1 = true
   return true
endif
if +reach<noun1 = true
   return true
endif
write "Tearing that would achieve little.^"
set time = false
}

grammar search *present >search

{+search
if +important<noun1 = true
   return true
endif
if +darkness = true
   return true
endif
if +reach<noun1 = true
   return true
endif
write "You search " noun1{the} ", but find nothing of interest.^"
}

grammar wield *held >wield

{+wield
write "It is impossible to wield that.^"
set time = false
}

grammar unwield *held >unwield

{+unwield
write "You are not wielding it!^"
set time = false
}

{+wait
write "You look around idly, letting the time pass by.^"
}

{+default_take
if noun1(parent) has LOCATION
   write "You take " noun1{the} " from the ground."^
else
   write "You take " noun1{the} " from " noun1(parent){the} .^
endif
move noun1 to player
ensure noun1 has TOUCHED
}

{+score
set time = false
if total_moves = 1
  if score = 1
  write "Your score is " score " point in 1 move.^"
else
write "Your score is " score " points in 1 move.^"
endif
else
if score = 1
  write "Your score is " score " point in " total_moves " moves.^"
else
  write "Your score is " score " points in " total_moves " moves.^"
endif
endif
}

{+blankjacl
write "Running out of things to do, you do absolutely nothing.^"
set time = false
}

{+eachturn
if here(region) = Cellars
move north_wall to here
move south_wall to here
move east_wall to here
move west_wall to here
move ceiling to here
move ground to here
endif
}

grammar stand behind *held >stand_behind
grammar hold *held in front of me >stand_behind

{+stand_behind
if noun1 = loose_point
write "It's a good idea, though your back would be unprotected.^"
return
else
write "You hold " noun1{the} " before yourself for a while. Other than your hands getting numb, nothing obvious seems to happen.^"
return
endif
}

grammar wrap *held around *here >wrap

{+wrap
if noun1 = loose_point
write "After a few attempts to wrap it around yourself, you realise that it's too big to wear like that.^"
return
else
write "There's no point in wrapping " noun1{the} " around " noun2{the} .^
return
endif
}

{+push
if +important<noun1 = true
   return true
endif
if +darkness = true
   return true
endif
if +reach<noun1 = true
   return true
endif
override
write "You can't push that.^"
}

grammar crawl under *present >crawl_under

{+crawl_under
if +important<noun1 = true
   return true
endif
if +darkness = true
   return true
endif
if +reach<noun1 = true
   return true
endif
override
write "You can't crawl under that!^"
}

grammar converse with *present >converse

{+converse
if noun1 hasnt ANIMATE
write "But you can't converse with that!^"
else
write "You can't think of anything to say.^"
endif
}

{+cut_with
if +important<noun1 = true
   return true
endif
if +darkness = true
   return true
endif
if +reach<noun1 = true
   return true
endif
if noun2 = dagger
write "You can't go around cutting everything with the dagger!^"
set time = false
return
endif
write "Vandal!^"
}

{+drop
if +move_scenery<noun1 = true
   return true
endif
if +worn<noun1 = true
   return true
endif
override
if here has MID_WATER : here has ON_WATER
   write "You drop " noun1{the} " which rapidly sink" noun1{s} " out of "
   write "view.^"
   move noun1 to limbo
   return true
endif
if here = midair
write "You drop " noun1{the} ", which disappear" noun1{s} " "
if noun1(mass) < 50
write "slowly "
else
write "rapidly "
endif
write "from sight.^"
move noun1 to guard_room
return
endif
write "You drop " noun1{the} .^
move noun1 to here
}

integer OFFSET
integer INDEX
string status_text "temp"

{+update_status_window
execute "+reverse_window"
style reverse
cursor 1 0
if total_moves = -1
   write "Eria by Parham Doustdar and Robert Osztolykan"
   style normal
   return
endif
write here{The}
setstring status_text "Score: " score " Moves: " total_moves
set OFFSET = status_width
length INDEX status_text
set OFFSET - INDEX
set OFFSET - 1
cursor OFFSET 0
write status_text
style normal
}

{+object_info
if noun3 has WORN
   write " (being worn)"
endif
if noun3 has LUMINOUS
   write " (providing light)"
endif
if noun3 has ON
   write " (on)"
endif
ifall noun3 has CLOSABLE : noun3 hasnt CLOSED
   write " (open)"
endif
if noun3 has WIELDED
write " (wielded)"
endif
}

#include "utils.library"
#include "verbs.library"
#include "menu.library"
#debug "debug.library"
