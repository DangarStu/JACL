# HEADERS AND FOOTERS FOR WEB-BASED APPLICATIONS

string current_image "none"

parameter "mode" WEBAPP_mode
integer WEBAPP_mode 1

integer WEBAPP_index

parameter "device" WEBAPP_device
integer WEBAPP_device	1
constant WEBAPP_desktop	1
constant WEBAPP_mobile	2

grammar change mode $integer >change_mode

{+change_mode 
set WEBAPP_mode = $integer
}

grammar change device $integer >change_device

{+change_device
set WEBAPP_device = $integer
}

{+header
set linebreaks = false
print:
<!DOCTYPE html>
<html dir="ltr" lang="en-US">
	<head>
		<meta HTTP-EQUIV="Content-Type" CONTENT="text/html; charset=UTF-8">
		<meta name="viewport" content="width=device-width, initial-scale=1.0">
.
write "<title>" game_title "</title>"
execute "+styles"
call "+user_styles"
write "<script src=~/include/jquery-3.6.0.min.js~></script>"

ifstring favicon_image = "none"
	write "<link rel=~icon~ href=~/images/favicon.ico~ type=~image/x-icon~>"
else
	write "<link rel=~icon~ href=~" favicon_image "~ type=~image/x-icon~>"
endif
print:
   <script language=~JavaScript~>^
	if (window.matchMedia("(max-width: 767px)").matches) {
.
		if WEBAPP_device != WEBAPP_mobile
         		write "window.location.replace(~" $url "?device=2~);"
		endif
print:
	}

	if (window.matchMedia("(min-width: 767px)").matches) {
.
		if WEBAPP_device != WEBAPP_desktop
         		write "window.location.replace(~" $url "?device=1~);"
		endif
write "}"

#include "ajax.library"

write "</script>^"
call "+local_header"
print:  
   </head><body>^
   <div id="header">
.
ifstring title_image = "none"
  write "<h1 class=~title~>" game_title "</h1>"
else
  if WEBAPP_device = WEBAPP_desktop
     write "<img class=~titleimage~ alt=~" game_title "~ src=~" desktop_title_image "~>"
  else
     write "<img class=~titleimage~ alt=~" game_title "~ src=~" mobile_title_image "~>"
  endif
endif
call "+header_links"
write "<div class=~linkbar~>
write "</div>^"
write "<div class=~links~>
call "+links_div"
write "</div>"
write "</div>^"
if @tab_names > 1
   write "<div class=~mode~>^"
   set WEBAPP_index = 1 
   while WEBAPP_index < @tab_names 
     if WEBAPP_mode = WEBAPP_index
        write "<div class=~selectedtab~>" tab_names[WEBAPP_index] "</div>"
     else
        write "<div class=~tab~>"
        write "<a class=~tablink~ href=~" $url "?"
        if remote_user = false
	      write "user_id=" $user_id "&"
        endif
	   write "mode=" WEBAPP_index "~>" tab_names[WEBAPP_index] "</a>"
	   write "</div>
     endif
     set WEBAPP_index + 1
   endwhile
   write "</div>"
endif
write "<div id=~main~>^"
set linebreaks = true
}

{+footer
write "</div>"
write "</body></html>^"
}

{+command_link
write "<a href=~" $url "?command=" string_arg[0]
if remote_user = false
   write "&user_id=" $user_id
endif
write "~>"
}

