# HEADERS AND FOOTERS FOR WEB-BASED APPLICATIONS

string current_image "none"

integer WEBAPP_mode 1
integer WEBAPP_index

parameter "mode" WEBAPP_mode

grammar change mode $integer >change_mode

{+change_mode 
set WEBAPP_mode = $integer
}

grammar standard >standard

{+header
set linebreaks = false
print:
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"^
	"http://www.w3.org/TR/html4/loose.dtd">^
	<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en-US">
	<head>
		<meta HTTP-EQUIV="Content-Type" CONTENT="text/html; charset=UTF-8">
		<meta name="viewport" content="width=device-width,user-scalable=no,initial-scale=1.0,maximum-scale=1.0">
.
write "<title>" game_title "</title>"
execute "+styles"
call "+user_styles"
write "<script src=~/include/jquery-3.6.0.min.js~></script>"

ifstring favicon_image = "none"
	write "<link rel=~icon~ href=~/images/favicon.ico~ type=~image/x-icon~>"
else
	write "<link rel=~icon~ href=~" favicon_image "~ type=~image/x-icon~>"
endif
print:
   <script language=~JavaScript~>^
      function ajaxRequest(event) {^
.
         write "var url_string = ~" $url
         if remote_user = false
            write "?user_id=" $user_id
         endif
         write ""&ajax=true&command=~ + "
         write "event.data.command;^"
         write "$.ajax({^url: url_string,^"
print:
             success: function (data) { 
			if (event.data.replace == true) {
				$(event.data.element).html(data);
			} else {	
				$(event.data.element).append(data);
			}
			call "+local_success"
		  }
         });^
      }^
   </script>^
.
call "+local_header"
print:  
   </head><body>^
   <div id="header">
.
ifstring title_image = "none"
  write "<h1 class=~title~>" game_title "</h1>"
else
  write "<img class=~titleimage~ alt=~" game_title "~ src=~" title_image "~>"
endif
call "+header_links"
write "<div class=~links~>
call "+links_div"
write "</div>^"
write "</div>^"
if @tab_names > 1
	write "<div class=~mode~>^"
    set WEBAPP_index = 1 
    while WEBAPP_index < @tab_names 
  	   if WEBAPP_mode = WEBAPP_index
	      write "<div class=~selectedtab~>" tab_names[WEBAPP_index] "</div>"
	   else
	      write "<div class=~tab~>"
	      write "<a class=~tablink~ href=~" $url "?"
		 if remote_user = false
		    write "user_id=" $user_id "&"
		 endif
	      write "command=change mode " WEBAPP_index "&mode=" WEBAPP_index "~>" tab_names[WEBAPP_index] "</a>"
	      write "</div>
	  endif
      set WEBAPP_index + 1
    endwhile
	write "</div>"
endif
write "<div id=~main~>^"
set linebreaks = true
}

{+footer
set linebreaks = false
write "</div>"
write "</body></html>^"
set linebreaks = true
}
